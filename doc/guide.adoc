= Task Manual
Antoine Kalmbach <ane@iki.fi>
v0.2.0
:toc: left
:numbered:
:idseparator: -
:idprefix:
:sectlinks:
:sectnums:
:sectanchors:
:source-highlighter: pygments
:pygments-style: default
:sourcedir: ../src/examples/clj/task

image:https://img.shields.io/clojars/v/com.github.ane/task.svg[link="https://clojars.org/com.github.ane/task"] image:https://img.shields.io/circleci/project/github/ane/task.svg[link=https://circleci.com/gh/ane/task/tree/master] image:https://img.shields.io/badge/License-Apache%202.0-blue.svg[link="http://www.apache.org/licenses/LICENSE-2.0"]

[.lead]
Welcome to the official Task user guide!

* https://github.com/ane/task[Github Project]
* https://ane.github.io/task/api[API Documentation]

[[overview]]
== Overview

This document is a reference documentation for Task, a Clojure library for asynchronous computation.

=== What is Task?

Tasks provide simple and functional concurrency primitives for Clojure.
Tasks represent asynchronous computation as __pure values__, and favours
function composition over
https://en.wikipedia.org/wiki/Callback_(computer_programming)[callbacks]
or promises.

Task promotes the use of combinators like `then` and `compose`.
These let you combine asynchronous data functionally, so that
asynchronous steps are just transformations on a value.

=== Key features

* **Value-oriented**: tasks are just eventual values. No more callbacks,
regular `deref`/`@` is all you need.
* **Functional**: tasks are composable. Tasks come with a set of
operations that lets you compose and combine them in a functional
manner.
* **Interoperable**: Any Clojure `future`/`promise` can be converted
into a task, and vice versa.
* **Asynchronous**: tasks are always asynchronous. Tasks default to the
https://docs.oracle.com/javase/8/docs/api/java/util/concurrent/ForkJoinPool.html?is-external=true[ForkJoinPool]
executor.
* **Customizable**: If you wish to control the execution model, you can
define your own
https://docs.oracle.com/javase/7/docs/api/java/util/concurrent/ExecutorService.html[ExecutorService].
* **Performant**: The library leverages the standard
https://docs.oracle.com/javase/8/docs/technotes/guides/concurrency/changes8.html[Java
8 Concurrency API], a powerful framework for concurrent computation.

=== An example

For example, a potential use case for asynchronous computation is doing HTTP requests. 
If we want to operate on the value of a GET request, and apply a data transformation,
this is what you need to do.

[source,clojure]
----
(def myreq 
  (task/then
    (fn [data] (->> data                          ;; <2>
                    cheshire/parse-string         ;; <3>
                    :my-interesting-attribute     ;; <3>
                    clojure.string/upper-case))   ;; <3>
    (http/get "http://some.api/endpoint"))        ;; <1>

(println @myreq)                                  ;; <4>
----
<1> We launch a HTTP GET request asynchronously using http://www.http-kit.org/[http-kit]. The 
    promise returned by it is converted into a task automatically.
<2> We pass the task and a function to `then`, a function described in <<composing-tasks>>. The *function* comes
    first and then comes the task.
<3> We parse the JSON data using Cheshire, extract an attribute and uppercase it.
<4> We deref the value of hte task which blocks the current thread until the task is complete.

This whole value executes in another thread (depending on the
link:./03-executors.md[execution model]) and _eventually_ resolves to a
value as it is `deref` d.

The snippet uses http://www.http-kit.org/[http-kit] to launch a GET
request asynchronously (<1>). The http-kit promise is automatically converted
into a task. Once the promise completes, we parse the string into JSON
(`cheshire/parse-string`) and then extract our attribute from it, and
then uppercase the results.

All of this is executed asynchronously, so we wrap the call to `then`
with a call to `@`, which calls
http://clojuredocs.org/clojure.core/deref[deref], a Clojure standard
library function, to _await_ its result in the current thread. This
blocks the execution of the current thread an and returns the result of
the computation.

Head over to the link:./02-guide.md[User Guide] to learn more!

=== Installation & Requirements

You need Java 8 to use Task, because Task uses features that were introduced in that version. To install Task, just add

[source,clojure]
----
[com.github.ane/task "0.2.0"]
----

to your Leiningen or Boot project dependency section.

[[creating-tasks]]
== User Guide

=== Creating tasks

To create a task, use [[run]]. It evaluates its result asynchronously.
It behaves more or less like http://clojuredocs.org/clojure.core/do[do]
except that it, depending on the
https://ane.github.io/task/03-executors.html[execution model], may
evaluate its result in another thread.

[source,clojure]
----
(task/run (println "hello")
          (Thread/sleep 1000)
          123)
----

This task evaluates to 123. We can get it value by `deref`ing it.

To get the value of the task, use `deref` or `@` from the Clojure
standard library. This blocks the current thread.

[source,clojure]
----
; these are both equal
@(task/run 123) ; => 123

(deref (task/run 123)) ; => 123
----

_See the docs on http://clojuredocs.org/clojure.core/deref[deref]._

Calling `(run 123)` results possibly in the creation of
another thread. To create an "immeadiate" value that doesn't cause any
unwanted execution, use `now`:

[source,clojure]
----
@(task/now 123)
----

[[void]] To create an empty task you can use `(void)` which is a task that never
completes.

If you want to use another executor, you have two options. You can use `run-in` which accepts
an explicit Executor parameter.

[source,clojure]
----
(let [pool (Executors/newFixedThreadPool 16)]
  (task/run-in pool
    (Thread/sleep 1000)
    123)
----

A more Clojurelike approach is to use *dynamic binding*. The namespace variable `task/\*pool*` is the default
ExecutorService in scope. Rebinding `\*pool*` to the executor of your choosing is done as follows:

[source, clojure]
----
(binding [task/*pool* (Executors/newFixedThreadPool 1)]
  (task/run (Thread/sleep 1000)
            (println "hello!")))
----

This approach, dynamic binding, lets you do executor assignment in a much more idiomatic fashion. See also <<using-executors>>
and the <<execution-model>> for more information about executors.

[[composing-tasks]]
=== Composing tasks

[[function-application-then]]

==== Function application: `then`

If you want to apply a function on the value returned by a task, use
`then`:

[source,clojure]
----
(task/then clojure.string/upper-case (task/run "asdf"))
; => ASDF
----

`then` produces another task, so you can deref its result:

[source,clojure]
----
@(task/then inc (task/run 123))

; => 124
----

If you want to apply a function that produces another task, use
`compose`:

[source,clojure]
----
@(task/compose (fn [x] (task/run
                         (Thread/sleep 1212)
                         (inc x)))
               (task/run 778))
----

If you had used `then` the result would have been a task inside another
task.

[[interoperability]]
=== Interoperability

Standard Clojure `future` and `promise` are compatible with tasks:

[source,clojure]
----
@(then inc (future 9)) ; => 10

(def foo (promise))

(then println foo)

(deliver foo "hello")

; prints "hello"
----

[[using-executors]]
=== Using custom executors

By default, the `\*pool*` variable is bound to the default executor known as ForkJoinPool. If you want to
use your own executor, there are two approaches:

  1. Use http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/binding[dynamic binding] to set `\*pool*` to
the executor of your choice.
  2. [[explicit-variants]]Use the explicit variants of the combinators which accept an executor as the third parameter. These are:
     * https://ane.github.io/task/api/task.core.html#var-run-in[run-in]
     * https://ane.github.io/task/api/task.core.html#var-then-in[sequence-in]
     * https://ane.github.io/task/api/task.core.html#var-compose-in[compose-in]
     * https://ane.github.io/task/api/task.core.html#var-for-in[for-in]
     * https://ane.github.io/task/api/task.core.html#var-sequence-in[sequence-in]
     * https://ane.github.io/task/api/task.core.html#var-traverse-in[traverse-in]

==== Using dynamic binding

Rebind `\*pool*` like this:

[source,clojure]
----
include::{sourcedir}/tutorial.clj[tag=rebind1,indent=0]
----

In the scope, and subsequent calls to, the core functions, the default executing pool will be the binding.

==== Using explicit executors

All core combinators like `then` and `compose` accept a third parameter as the
https://docs.oracle.com/javase/7/docs/api/java/util/concurrent/ExecutorService.html[ExecutorService] in their
<<explicit-variants, explicit variants>>:

[source,clojure]
----
(let [pool (Executors/newFixedThreadPool 4)]
  (task/then-in inc (task/run-in 123 pool) pool))
----

This approach can be used to define multiple thread pools and use them in the
chain of a task execution, spreading execution across multiple executors. 

[source,clojure]
----
include::{sourcedir}/tutorial.clj[tag=multiple-executors,indent=0]
----

[[using-for-to-avoid-boilerplate]]
==== Using `for` to avoid boilerplate

The [[for]] macro, like its standard library namesake, lets you skip
some boilerplate when working with tasks that depend on each other:

[source,clojure]
----
include::{sourcedir}/tutorial.clj[tag=for,indent=0]
----

`for` evaluates its body once all futures are complete, and the values
of each future are bound to their respective bindings.

[[lists-of-tasks]]
=== Working with sequences

If you have a sequence of tasks, and you want to deal with each result
as a whole, you can turn a sequence of tasks into a task that evaluates
into the values of each task inside it using [[sequence]]:

[source,clojure]
----
include::{sourcedir}/tutorial.clj[tag=sequence1,indent=0]
----

`sequence` completes when all the tasks complete.

Similarly, if you have a funtion that produces a task, you can apply it to
a sequence and gather its results into inside one task.

[source,clojure]
----
include::{sourcedir}/tutorial.clj[tag=traverse1,indent=0]
----

Using regular `map` would have just produced a sequence of tasks, not what we want.

[[completion]]
=== Completion

To check if a task is complete, use `done?`:

[source,clojure]
----
(task/done? some-task)

(task/done? (task/run (Thread/sleep 123123) 'foo))
; => false

(task/done? (task/now 1))
----

To complete a task before it is done, use [[complete!]]:

[source,clojure]
----
(def baz (task/run (Thread/sleep 10000) 'foo))

(task/complete! baz 'bar)

@baz ; => 'bar
----

If the task is already complete, it does nothing.

To get a value anyway if the task isn't complete, use [[else]]:

[source,clojure]
----
(else (task/run (Thread/sleep 1000) 1) 2)

; => 2
----

[[forcing-a-result]]
==== Forcing a result

To force the result of a task, completed or not, use [[force!]]:

[source,clojure]
----
(def t (task/now 123))

(task/force! t 'hi)

@t ; => 'hi
----

[[cancellation]]
==== Cancellation

To cancel a task, use [[cancel]]:

[source,clojure]
----
(def my-task (task/run (Thread/sleep 10000) 'bla))

(task/cancel my-task)
----

To see if the task was cancelled, use [[cancelled?]]:

[source,clojure]
----
(cancelled? my-task) ; => true
----

Using `deref` on a cancelled task blows up, predictably.

[[failures]]
==== Failures

A task is said to have _failed_ if its evaluation produced an exception
or it produced an exception during its execution. Such a task is a
cancelled task (see link:#Cancellation[Cancellation]), or any task that
produces an exception when `deref`'d:

[source,clojure]
----
(def oops (task/run (throw (RuntimeException. "hey!"))))

@oops

; RuntimeException hey!  task.core/fn--17494 (form-init7142405608168193525.clj:182)
----

[[failed? ]]will tell you if that task has failed:

[source,clojure]
----
(task/failed? oops) ; => true
----

To create a failed task with some exception, use [[failed]]:

[source,clojure]
----
(def failed-task (task/failed (RuntimeException. "argf")))
----

To get the exception that caused the failure, use [[failure]]:

[source,clojure]
----
(task/failure failed-task) ; => RuntimeException[:cause "argf"] 
----

To force a task to fail, like [[force!]], use [[fail!]]:

[source,clojure]
----
(def foo (task/now "hi there"))

(task/fail! foo (IllegalStateException. "poop"))

(task/failed? foo) ; => true

(task/failure foo) ; => IllegalStateException[:cause "poop"]
----

Chaining a failed task to a normal task will cause the resulting task to
fail.

[[recovering-from-errors]]
=== Error handling

To recover from errors, use `recover`:

[source,clojure]
----
(def boom (task/run (/ 1 0)))

(def incremented (task/then inc boom))
----

This will blow up, so we can ensure that the resulting operation
succeeds:

[source,clojure]
----
@(recover incremented
          (fn [ex]
            (println "caught exception: " (.getMessage ex))
            123))

; caught exception: java.lang.ArithmeticException: Divide by zero
; => 123
----

So you can recover from potential failures using a backup value from the
function. Supplying a non-function will just recover with that value:

[source,clojure]
----
@(recover boom "hello") ; => "hello"
----

== Advanced topics

[[execution-model]]
=== Execution model

In the task model, the execution model is intentionally kept opaque, but
not hidden. This means that by default, the user doesn't need to worry
about where -- in which thread -- code is executed.

What the user needs to know, to __get started__, is:

* all code is asynchronous and non-blocking, and
* `deref` or `@` will block the __current thread__.

The concurrency model is left to the __executor__.

A JVM executor is an abstraction for thread execution. This can be a
thread pool with 64 threads per core. Or more. Or less. By default, task
uses
https://docs.oracle.com/javase/8/docs/api/java/util/concurrent/ForkJoinPool.html[ForkJoinPool],
a work-stealing scheduler, that attempts to find the best use for each
of its threads.

The reason to default to the ForkJoinPool, in addition for it being
perfomant, is that it kind of makes tasks appear lightweight (a bit
like, but not similar to, fibers), as the user does not intentionally
think that there is a one-to-one correspondence between each task and a
thread. This means that you can have several hundred tasks executing
concurrently for only a handful of threads! The ForkJoinPool handles the
heavy lifting here: some tasks will complete faster, some slower, so the
point is to have this work-stealing algorithm that makes use of idle
threads.

It is often the case that such behavior is not desirable. Which is why
the executor can be overridden via two methods:

* implicitly by using dynamic binding, or
* explicitly by using parameters to task-executing functions.

Dynamic binding is a standard Clojure method for doing dynamic scope.
This is how it works.

[source,clojure]
----
(def ^:dynamic foo "hello")

(println foo) ; prints "hello"

(binding [foo "hi"]
  (println foo)) ; prints "hi"
----

Using this technique lets us freely swap the executor. The var for this
is `\*pool*`. See the example in <<using-executors>>.

The JVM offers
https://docs.oracle.com/javase/8/docs/api/java/util/concurrent/ExecutorService.html[several]
you can freely chose from. This document is not a guide on their
details, the user is left to explore that page or the Internet for more
examples.

Usually, in 99% of cases, the default `ForkJoinPool` executor is just
fine. But the option to customize is there.

[[blocking-pitfalls]]
=== Beware of blocking

To get the value out of a task, you deref it. This blocks the executing
thread. It is generally safe to do so, but choosing the wrong kind of
executor might yield strange behaviour.

For example, when using a
https://docs.oracle.com/javase/8/docs/api/java/util/concurrent/Executors.html#newFixedThreadPool-int-[fixed
thread pool] like this, it might not be obvious that once you run out of
threads, the whole program will grind to a halt.

Blocking inside a task will _block that task_. So if you have a special kind of thread pool that
may run out of threads, this is the part where we'd say, *here be dragons*.

Here is an example that will go awry.

[source,clojure]
----
include::{sourcedir}/tutorial.clj[tag=whoops,indent=0]
----

The function that we pass to compose tries to deref `nothing`, which is a task that will never
complete (see <<void,`void`>>). The executor we've defined has only one thread in its thread pool,
so it cannot launch a new thread for the surrounding task resulting from the call to `compose`.

How is this relevant? This is particularly nasty if you do it inside a HTTP server, e.g. you
use the same thread pool for handling incoming web requests and database
access. If your database hangs and takes time to answer the questions,
the web server, which shares the thread pool, cannot spawn new threads
to handle incoming requests and the web server will appear unresponsive!
